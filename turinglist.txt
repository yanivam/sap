.start Setup
State: .Integer #0
Head: .Integer #0
Tape: .String "_101010_"
InputChar: .Integer #0
OutputChar: .Integer #0
Direction: .Integer #0
HeadPos: .Integer #0   ;holds position of head in whole memory
TupMatch: .Integer #0	  ;holds # of correct tuple
NumTuples: .Integer #5
Tuples: .tuple \ 0 _ 0 _ r \
	   .tuple \ 0 0 1 1 r \
	   .tuple \ 0 1 1 0 r \
	   .tuple \ 1 1 1 0 r \
	   .tuple \ 1 0 1 1 r \
NewLine: .Integer #10
StartMess: .String "Running Turing Machine"
TupMess: .String "Tuples:"
BreakLine: .String "_______________________"
StateMess: .String "Initial State: "
HeadMess: .String "Initial Head Position: "
TapeMess: .String "Initial Tape: "
CS: .String "CS: "
I: .String " I: "
NS: .String " NS: "
O: .String " O: "
D: .String " D: "
OutBoundsMess: .String "Out of bounds. Program Terminated."
EndMess: .String "Final Tape: "
StepsMess: .String "Number of steps taken: "

;purposes of each register:
	;0: holds newLine ASCII
	;1: holds tuple number
	;3: holds address of first tuple
	;4: holds number of steps
	;all others placeholders for various computations

Setup: movmr State r6   ;setup variables for beginning message printout
	movmr Head r7
	movmr NewLine r0
	movir #0 r1
	movir #0 r4
	movar Tuples r3
	outs StartMess
	outcr r0
	outcr r0
	outs TupMess
	outcr r0
	jmp PrintTuples
	
PrintTuple: movrr r1 r9   ;prints one tuple
	mulir #5 r9
	addrr r9 r3   ;go to spot of tuple to be printed
	movxr r3 r9
	printi r9   ;current state
	outci #9   ;ASCII for horizontal tab
	addir #1 r3
	movxr r3 r9
	outcr r9   ;input character
	outci #9
	addir #1 r3
	movxr r3 r9
	printi r9   ;new state
	outci #9
	addir #1 r3
	movxr r3 r9
	outcr r9   ;output character
	outci #9
	addir #1 r3
	movxr r3 r9
	sojz r9 PrintR   ;jump to print r if direction is 1, continue to print l
	outci #108   ;ASCII for l
	outcr r0
	movar Tuples r3   ;reset tuple address for next time subroutine is called
	ret
	
PrintR: outci #114   ;ASCII for r
	outcr r0
	movar Tuples r3
	ret
	
ToPrint: jsr PrintTuple   ;extra step before loop to start incrementing tuple number only after first compare
	addir #1 r1

PrintTuples: cmpmr NumTuples r1   ;loops to print out all tuples
	jmpne ToPrint
	outcr r0

BeginMess: outs BreakLine   ;beginning message
	outcr r0
	outs StateMess
	printi r6
	outcr r0
	outs HeadMess
	printi r7
	outcr r0
	outs TapeMess
	outs Tape
	outcr r0
	outcr r0

Run: jsr GetInput   ;loop to execute Turing program
	jsr FindTuple
	jsr TraceLine
	jsr UpdateSystem
	jmp Run

GetInput: movar Tape r8   ;saves new input character to be used to find tuple match
	addir #1 r8   ;first character of tape
	movmr Head r7
	addrr r7 r8   ;get to current place in tape
	movrm r8 HeadPos
	movxr r8 r9   ;get character at location and put in InputChar label
	movrm r9 InputChar
	ret

FindTuple: movir #0 r1   ;resets loop variables before jumping to loop
	movar Tuples r3
	jsr LoopSearch
	movar Tuples r3
	ret
	
LoopSearch: cmpmr NumTuples r1   ;loop to search for tuple
	jmpne CheckTuple
	jmp End   ;if no tuple found, end execution

CheckTuple: movrr r1 r9   ;compares current state to tuple
	mulir #5 r9
	addrr r9 r3   ;go to memory location of tuple
	movxr r3 r9
	cmpmr State r9
	jmpz CheckInput   ;if state matched, check input character, else move to next tuple
	addir #1 r1
	movar Tuples r3
	jmp LoopSearch
	
CheckInput: addir #1 r3   ;compares input character to tuple
	movxr r3 r9
	cmpmr InputChar r9
	jmpz SaveTuple   ;if IC matched, save tuple, otherwise move to next tuple
	addir #1 r1
	movar Tuples r3
	jmp LoopSearch
	
SaveTuple: movrm r1 TupMatch   ;saves correct tuple
	addir #1 r4   ;increments number of steps in execution
	ret

TraceLine: jsr PrintTape   ;prints trace line
	jsr PrintTupMatch
	ret
	
PrintTape: movar Tape r5   ;prints tape with brackets around current head
	movmr HeadPos r6
	movar Tape r7
	addxr r5 r7   ;end of tape
	jsr BeforeHead
	jsr AtHead
	jsr AfterHead
	outci #9   ;ASCII for horizontal tab
	ret
	
BeforeHead: addir #1 r5
	subrr r5 r6
	outcb r5 r6
	ret
	
AtHead: outci #91   ;ASCII for left bracket
	outcx r6
	outci #93   ;ASCII for right bracket
	ret
	
AfterHead: subrr r6 r7
	addir #1 r6
	outcb r6 r7
	ret
	
PrintTupMatch: movmr TupMatch r9   ;prints tuple associated with current state and input character
	mulir #5 r9
	addrr r9 r3   ;go to spot of tuple to be printed
	movxr r3 r9
	outs CS
	printi r9   ;current state
	addir #1 r3
	movxr r3 r9
	outs I
	outcr r9   ;input character
	addir #1 r3
	movxr r3 r9
	outs NS
	printi r9   ;new state
	addir #1 r3
	movxr r3 r9
	outs O
	outcr r9   ;output character
	addir #1 r3
	movxr r3 r9
	sojz r9 PrintTMR   ;jump to print r if direction is 1, continue to print l
	outs D
	outci #108
	outcr r0
	movar Tuples r3   ;reset tuple address for next time subroutine is called
	ret
	
PrintTMR: outs D   ;prints tuple match direction r
	outci #114
	outcr r0
	movar Tuples r3
	ret

UpdateSystem: jsr EnterTuple   ;changes tape and variables according to new tuple
	jsr ChangeTape
	jsr CheckBounds
	ret

EnterTuple: movmr TupMatch r2   ;saves new variables of tuple
	mulir #5 r2
	addrr r2 r3
	addir #2 r3   ;at new state of saved tuple
	movxr r3 r2
	movrm r2 State
	addir #1 r3
	movxr r3 r2
	movrm r2 OutputChar
	addir #1 r3
	movxr r3 r2
	movrm r2 Direction
	ret
	
ChangeTape: movmr HeadPos r6   ;manipulates tape with new saved variables
	movmr Head r7
	movmr Direction r8
	movmr OutputChar r9
	movrx r9 r6   ;replace character at head position
	addrr r8 r7   ;shift head
	movrm r7 Head
	ret
	
CheckBounds: movmr Tape r5   ;makes sure head position did not stray out of bounds of tape
	movmr Head r7
	cmpir #0 r7
	jmpp PrintError   ;if head is less than location 0, compare > 0, out of bounds
	cmprr r5 r7
	jmpn PrintError   ;if head is greater than tape length, compare < 0, out of bounds
	ret
	
PrintError: outs OutBoundsMess   ;prints error if head is out of bounds
	
End: outcr r0   ;prints end of run messages
	outs EndMess
	outs Tape
	outcr r0
	outs StepsMess
	printi r4
	outcr r0
	halt
	.end
